{
  lib,
  pkgs,
  config,
  ...
}:
with lib;
let
  cfg = config.services.sentinelone;
  customerId =
    cfg.customerId or (
      if cfg.email != null && cfg.serialNumber != null then "${cfg.email}-${cfg.serialNumber}" else null
    );
  hasCustomerId = customerId != null;
  initScript = pkgs.writeShellScriptBin "sentinelone-init.sh" ''
    #!/bin/bash

    mkdir -p ${cfg.dataDir}

    # initialize the data directory
    if [ -z "$(ls -A ${cfg.dataDir} 2>/dev/null)" ]; then
      find "${cfg.package}/opt/sentinelone/" -mindepth 1 -maxdepth 1 ! -name "bin" ! -name "ebpfs" ! -name "ranger" -exec cp -r {} "${cfg.dataDir}/" \;

      cat << EOF > ${cfg.dataDir}/configuration/install_config
    S1_AGENT_MANAGEMENT_TOKEN=$(cat ${cfg.sentinelOneManagementTokenPath})
    S1_AGENT_DEVICE_TYPE=desktop
    S1_AGENT_AUTO_START=true
    ${optionalString hasCustomerId "S1_AGENT_CUSTOMER_ID=${customerId}"}
    EOF

      cat << EOF > ${cfg.dataDir}/configuration/installation_params.json
    {
      "PACKAGE_TYPE": "deb",
      "SERVICE_TYPE": "systemd"
    }
    EOF
      siteKey=$(cat ${cfg.sentinelOneManagementTokenPath} | base64 -d | ${getExe pkgs.jq} .site_key)
      mgmtUrl=$(cat ${cfg.sentinelOneManagementTokenPath} | base64 -d | ${getExe pkgs.jq} .url)
      cat << EOF > ${cfg.dataDir}/configuration/basic.conf
    {
        "mgmt_device-type": 1,
        "mgmt_site-key": $siteKey,
        "mgmt_url": $mgmtUrl
    }
    EOF

      chown -R sentinelone:sentinelone ${cfg.dataDir}
      chmod -R 0755 $(find ${cfg.dataDir} -group sentinelone)
    fi
  '';
  sentinelctlFhs = pkgs.buildFHSEnv {
    name = "sentinelctl";
    runScript = "/opt/sentinelone/bin/sentinelctl";
  };
  sentinelctlScript = pkgs.writeShellApplication {
    name = "sentinelctl";
    runtimeInputs = with pkgs; [
      config.systemd.package
      util-linux
      sentinelctlFhs
    ];
    text = ''
      if [ "$EUID" -ne 0 ]; then
        echo "$0 must be run with root privileges"
        exit 1
      fi

      exec nsenter --all \
        --target "$(systemctl show -P MainPID sentinelone)" \
        -- sentinelctl "$@"
    '';
  };
in
{
  options = {
    services = {
      sentinelone = {
        enable = mkEnableOption "SentinelOne Service";
        package = mkPackageOption pkgs "sentinelone" { };

        customerId = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = ''
            Set a customer specific identifier for the host. It is common practice to set this as your email and serial number separated by a hyphen.
          '';
          example = "me@gmail.com-FTXYZWW";
        };
        email = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = "Your email address, used to construct a unique customer ID. Deprecated in favour of customerId.";
          example = "me@gmail.com";
        };
        serialNumber = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = "Your host machines serial number, used to construct a unique customer ID. Deprecated in favour of customerId.";
          example = "FTXYZWW";
        };
        sentinelOneManagementTokenPath = mkOption {
          type = types.path;
          description = ''
            Path to file containing a SentinelOne management/site token. It is recommended to use a secret manager such a sops-nix or agenix.
          '';
          example = "/run/secrets/s1_mgmt_token";
        };
        dataDir = mkOption {
          type = types.path;
          description = "Directory in which the agent stores its runtime data.";
          default = "/var/lib/sentinelone";
        };
      };
    };
  };

  config = mkIf cfg.enable {
    warnings =
      optional (cfg.email != null) "services.sentinelone.email is deprecated in favour of customerId."
      ++ optional (
        cfg.serialNumber != null
      ) "services.sentinelone.serialNumber is deprecated in favour of customerId.";

    assertions = [
      {
        assertion = (cfg.customerId != null) -> (cfg.email == null && cfg.serialNumber == null);
        message = ''
          You cannot use services.sentinelone.customerId with the deprecated services.sentinelone.email and services.sentinelone.serialNumber options.
        '';
      }
      {
        assertion = (cfg.email != null) -> (cfg.serialNumber != null);
        message = ''
          services.sentinelone.email requires services.sentinelone.serialNumber to also be set.
        '';
      }
      {
        assertion = (cfg.serialNumber != null) -> (cfg.email != null);
        message = ''
          services.sentinelone.serialNumber requires services.sentinelone.email to also be set.
        '';
      }
    ];

    users.users.sentinelone = {
      isSystemUser = true;
      createHome = true;
      shell = "${pkgs.shadow}/bin/nologin";
      group = "sentinelone";
    };
    users.groups.sentinelone = { };

    systemd.tmpfiles.rules = [
      "d ${cfg.dataDir} 0755 sentinelone sentinelone -"
    ];

    systemd.services.sentinelone-init = {
      wantedBy = [ "sentinelone.service" ];
      before = [ "sentinelone.service" ];
      unitConfig.RequiresMountsFor = [ "/opt/sentinelone" ];
      serviceConfig = {
        Type = "oneshot";
        ExecStart = "${getExe initScript}";
      };
    };

    environment.systemPackages = [
      cfg.package
      sentinelctlScript
    ];

    systemd.mounts = [
      {
        what = cfg.dataDir;
        where = "/opt/sentinelone";
        type = "none";
        options = "bind";
        wantedBy = [ "sentinelone-init.service" ];
        before = [ "sentinelone-init.service" ];
      }
      {
        what = "${cfg.package}/opt/sentinelone/bin";
        where = "/opt/sentinelone/bin";
        type = "none";
        options = "bind,ro";
        requires = [ "opt-sentinelone.mount" ];
        after = [ "opt-sentinelone.mount" ];
        wantedBy = [ "sentinelone.service" ];
        before = [ "sentinelone.service" ];
      }
      {
        what = "${cfg.package}/opt/sentinelone/ebpfs";
        where = "/opt/sentinelone/ebpfs";
        type = "none";
        options = "bind,ro";
        requires = [ "opt-sentinelone.mount" ];
        after = [ "opt-sentinelone.mount" ];
        wantedBy = [ "sentinelone.service" ];
        before = [ "sentinelone.service" ];
      }
      {
        what = "${cfg.package}/opt/sentinelone/ranger";
        where = "/opt/sentinelone/ranger";
        type = "none";
        options = "bind,ro";
        requires = [ "opt-sentinelone.mount" ];
        after = [ "opt-sentinelone.mount" ];
        wantedBy = [ "sentinelone.service" ];
        before = [ "sentinelone.service" ];
      }
    ];

    systemd.services.sentinelone = {
      enable = true;
      description = "SentinelOne";
      path = [
        pkgs.coreutils-full
        pkgs.gawk
        pkgs.zlib
        pkgs.libelf
        pkgs.bash
      ];
      unitConfig = {
        Description = "SentinelOne";
        After = [
          "uptrack-prefetch.service"
          "uptrack.service"
        ];
        RefuseManualStop = "yes";
        StartLimitInterval = "90";
        StartLimitBurst = "4";
        RequiresMountsFor = [
          "/opt/sentinelone"
          "/opt/sentinelone/bin"
          "/opt/sentinelone/ebpfs"
          "/opt/sentinelone/lib"
          "/opt/sentinelone/ranger"
        ];
      };
      serviceConfig = {
        Type = "exec";
        ExecStart = "${cfg.package}/opt/sentinelone/bin/sentinelone-agent";
        WorkingDirectory = "/opt/sentinelone/bin";
        WatchdogSec = "30s";
        Restart = "on-failure";
        RestartSec = "4";
        ExecStop = "${lib.getExe sentinelctlFhs} control stop";
        MemoryAccounting = "yes";
        NotifyAccess = "all";
        TasksMax = "infinity";
      };
      wantedBy = [ "multi-user.target" ];
    };
  };
}
